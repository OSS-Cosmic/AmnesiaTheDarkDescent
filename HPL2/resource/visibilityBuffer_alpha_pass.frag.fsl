#include "scene_resource.h.fsl"
#include "packing.h.fsl"
#include "parallax_bindless.h.fsl"

STRUCT(PsIn)
{
        DATA(float4,     Position,  SV_Position);
        DATA(float3,     pos,       POSITION);
        DATA(float2,     uv,        TEXCOORD0);
#if !defined(INDIRECT_ROOT_CONSTANT)
        DATA(FLAT(uint), drawID,    TEXCOORD1);
#endif
        DATA(float3,     normal,    NORMAL);
        DATA(float3,     tangent,   TANGENT);
        DATA(float3,     bitangent, BITANGENT);
};

STRUCT(PsOut)
{
    DATA(float4, visibility, SV_Target0);
};

PsOut PS_MAIN(PsIn In, SV_PrimitiveID(uint) primitiveID)
{
    INIT_MAIN;
    PsOut Out;

    #if !defined(INDIRECT_ROOT_CONSTANT)
        UniformObject obj = Get(sceneObjects)[In.drawID];
    #else 
        UniformObject obj = Get(sceneObjects)[Get(indirectDrawId)];
    #endif


    uint materialIndex = MATERIAL_INDEX(obj.materialID);
    DiffuseMaterial diffuseMat = Get(sceneDiffuseMat)[materialIndex];  
  
    float diffuseAlpha = 1.0;

    float2 texCoord = In.uv;
    if(isTextureIndexValid(diffuseMat.heightTextureIndex)) {
        texCoord += ParallaxAdvance(
            In.uv, 
            0.0,
            32.0, 
            diffuseMat.heightMapScale * PARALLAX_MULTIPLIER,
            In.pos,
            In.normal,
            In.tangent,
            In.bitangent,
            diffuseMat.heightTextureIndex,
            Get(sceneFilters)[diffuseMat.samplerIndex],
            (diffuseMat.materialConfig & MATERIAL_IS_HEIGHT_SINGLE_CHANNEL) > 0);
    }


    float4 value; 
    if(SampleSceneTextureSampleIndexFloat4(diffuseMat.alphaTextureIndex,diffuseMat.samplerIndex, texCoord, value)) {
        if((diffuseMat.materialConfig & MATERIAL_IS_ALPHA_SINGLE_CHANNEL) > 0) {
            diffuseAlpha = value.r;
        } else {
            diffuseAlpha = value.a;
        }
    }

    if(obj.dissolveAmount < 1.0 || isTextureIndexValid(diffuseMat.alphaTextureIndex) || (diffuseMat.materialConfig & MATERIAL_USE_ALPHA_DISSOLVE_FILTER) > 0) {
        const float2 dissolveCoord = In.Position.xy * (1.0/128.0); //128 = size of dissolve texture.
        float fDissolve = SampleTex2D(Get(dissolveTexture), Get(nearPointWrapSampler), dissolveCoord).x;
        
        float4 alphaValue;
        if(SampleSceneTextureSampleIndexFloat4(diffuseMat.dissolveAlphaTextureIndex, diffuseMat.samplerIndex, texCoord, alphaValue)) {
            //Get in 0.75 - 1 range
            fDissolve = fDissolve * 0.25 + 0.75;
            float fDissolveAlpha = alphaValue.r;
            fDissolve -= (0.25 - fDissolveAlpha * 0.25);
        } else {
            //Get in 0.5 - 1 range.
            fDissolve = fDissolve*0.5 + 0.5;
        }
        diffuseAlpha = fDissolve -  (1.0 - (obj.dissolveAmount * diffuseAlpha)) * 0.5;
    }
    
    if(diffuseAlpha < 0.5) {
        discard;
    }
    
    #if !defined(INDIRECT_ROOT_CONSTANT)
        Out.visibility = unpackUnorm4x8(SCENE_VIZ_ALPHA_ID(In.drawID, primitiveID)); 
    #else 
        Out.visibility = unpackUnorm4x8(SCENE_VIZ_ALPHA_ID(Get(indirectDrawId), primitiveID)); 
    #endif
    RETURN(Out);
}


