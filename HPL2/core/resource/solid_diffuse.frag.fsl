#define MATERIAL_SOLID 1
#include "deferred_resources.h.fsl"

STRUCT(PsIn) 
{
    DATA(float4, Position, SV_Position);
    DATA(float3, pos, POSITION);
    DATA(float2, uv, TEXCOORD0);
    DATA(float3, view, TEXCOORD1);
    DATA(float3, normal, NORMAL);
    DATA(float3, tangent, TANGENT);
    DATA(float3, bitangent, BITANGENT);
};

STRUCT(PsOut)
{
    DATA(float4, diffuse, SV_Target0);
    DATA(float4, normal, SV_Target1);
    DATA(float4, position, SV_Target2);
    DATA(float2, specular, SV_Target3);
};

PsOut PS_MAIN(PsIn In)
{
    INIT_MAIN;
    PsOut Out;
    uint materialID = Get(uniformObjectBuffer)[Get(objectId)].materialID;

    float2 texCoord = In.uv.xy;
    float3 cameraEyeSpace = normalize(In.pos).xyz;

#ifdef PARALLAX_ENABLED
    if(HasHeight(Get(uniformMaterialBuffer)[materialID].textureConfig)) {
        texCoord.xy  = ParallaxRelief(
            materialID,
            SEARCH_SAMPLE_COUNT, 
            IsHeightMapSingleChannel(Get(uniformMaterialBuffer)[materialID].textureConfig), 
            Get(uniformMaterialBuffer)[materialID].heightMapScale * PARALAX_MULTIPLIER, 
            normalize(In.normal), 
            texCoord, 
            normalize(In.view));
    }
#endif

    float4 diffuseColor = DIFFUSE_SamplerTex2D(Get(diffuseMap), diffuseMapSamplerID(materialID), texCoord.xy);
    if(diffuseColor.w < ALPHA_REJECT ) {
        discard;
    }

    float3 normal = float3(0, 0, 0);
    if(HasNormal(Get(uniformMaterialBuffer)[materialID].textureConfig)) {
        float3 normalSample = DIFFUSE_SamplerTex2D(Get(normalMap), normalMapSamplerID(materialID), texCoord.xy).xyz - 0.5;
        normal = normalize(normalSample.x * In.tangent + normalSample.y * In.bitangent + normalSample.z * In.normal);
    } else {
        normal = normalize(In.normal);
    }

    if(HasCubeMap(Get(uniformMaterialBuffer)[materialID].textureConfig)) {
        float3 envUV = reflect(cameraEyeSpace, normal);
        envUV = mul(Get(invViewRotationMat), float4(envUV.xyz, 1.0)).xyz;
                    
        float4 reflectionColor = DIFFUSE_SampleTexCube(Get(cubeMap), cubeMapSamplerID(materialID), envUV);
        
        float afEDotN = max(dot(-cameraEyeSpace, normal),0.0);
        float fFresnel = Fresnel(afEDotN,
            Get(uniformMaterialBuffer)[materialID].frenselBias, 
            Get(uniformMaterialBuffer)[materialID].frenselPow);
        
        if(HasCubeMapAlpha(Get(uniformMaterialBuffer)[materialID].textureConfig)) {
            reflectionColor *= DIFFUSE_SamplerTex2D(Get(cubeMapAlpha), cubeMapAlphaSamplerID(materialID), texCoord.xy).wwww;
        }
        Out.diffuse = diffuseColor + reflectionColor * fFresnel;
    } else {
        Out.diffuse = diffuseColor;
    }
    Out.normal = float4(normal.xyz, 0.0);
    Out.position = float4(In.pos.xyz, 1.0);
    if(HasSpecular(Get(uniformMaterialBuffer)[materialID].textureConfig)) {
        Out.specular = DIFFUSE_SamplerTex2D(Get(specularMap), specularMapSamplerID(materialID), texCoord.xy).xy;
    } else {
        Out.specular = float2(0,0);
    }

    RETURN(Out);
}
